buildscript {
    ext.kotlin_version = rootProject.kotlinVersion
    repositories {
        jcenter()
        mavenCentral()
        mavenLocal()
        maven { url "http://dl.bintray.com/kotlin/kotlin-eap" }
        maven { url "http://dl.bintray.com/kotlin/kotlin-dev" }
        maven { url 'https://jitpack.io' }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlin_version}"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
        classpath "digital.wup:android-maven-publish:3.6.2"
    }
}

//Bintray

task sourceJar(type: Jar) {
    classifier 'sources'
    from android.sourceSets.main.java.srcDirs
}


apply plugin: 'maven-publish'
apply plugin: 'digital.wup.android-maven-publish'
publishing {
    publications {
        mavenAar(MavenPublication){
            from components.android
            artifact tasks.sourceJar
//            artifact tasks.dokkaJar
            groupId project.group.toString()
            artifactId project.name
            version project.version
        }
    }
}

def bintrayPropertiesFile = rootProject.file("bintray.properties")
if (bintrayPropertiesFile.exists()) {
    def bintrayProperties = new Properties()
    bintrayProperties.load(new FileInputStream(bintrayPropertiesFile))

    apply plugin: 'com.jfrog.bintray'
    bintray {
        user = bintrayProperties['bintrayUser']
        key = bintrayProperties['bintrayKey']
        publications = ['mavenAar']
        publish = bintrayProperties['bintrayPublish']
        pkg {
            repo = project.group.toString()
            name = project.name
            userOrg = bintrayProperties['bintrayOrganization']
            licenses = [bintrayProperties['bintrayLicense']]
            vcsUrl = bintrayProperties['bintrayVcs']
            websiteUrl = bintrayProperties['bintrayWebsite']
            issueTrackerUrl = bintrayProperties['bintrayIssues']
            version {
                name = project.version
                released = new Date()
            }
        }
    }
}
